plugins {
    id 'java-library'
    id "groovy"
    id 'maven-publish'
    id 'signing'
}

group 'dev.inaka'
version '1.0.2'

dependencies {
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.apache.httpcomponents:httpmime:4.5.13'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.json:json:20240303'
    testImplementation 'net.bytebuddy:byte-buddy:1.12.16'
    testImplementation 'org.objenesis:objenesis:3.2'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.13'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "sonatype"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = "${ossrhUsername}"
                password = "${ossrhPassword}"
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = 'dev.inaka'
            artifactId = 'jotenberg'
            version = '1.0.2'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Jotenberg'
                description = 'A Java library that interacts with Gotenberg\'s different modules to convert a variety of document formats to PDF files.'
                url = 'https://github.com/cherfia/jotenberg'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/cherfia/jotenberg/blob/main/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'cherfia'
                        name = 'Taha Abdelmoutaleb Cherfia'
                        email = 'taha.cherfia@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/cherfia/jotenberg.git'
                    developerConnection = 'scm:git:ssh://github.com/cherfia/jotenberg.git'
                    url = 'https://github.com/cherfia/jotenberg'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}