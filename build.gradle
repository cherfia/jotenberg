plugins {
    id 'java-library'
    id "groovy"
    id 'maven-publish'
}

group 'io.bitizens'
version '2.0.0'

dependencies {
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.httpcomponents:httpmime:4.5.14'
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'org.json:json:20240303'
    testImplementation 'net.bytebuddy:byte-buddy:1.14.17'
    testImplementation 'org.objenesis:objenesis:3.4'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.22'
    testImplementation 'org.spockframework:spock-core:2.4-M4-groovy-3.0'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/cherfia/jotenberg"
            credentials {
                username = project.findProperty("github_actor") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("github_token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.bitizens'
            artifactId = 'jotenberg'
            version = '2.0.0'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Jotenberg'
                description = 'A Java library that interacts with Gotenberg\'s different modules to convert a variety of document formats to PDF files.'
                url = 'https://github.com/cherfia/jotenberg'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/cherfia/jotenberg/blob/main/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'cherfia'
                        name = 'Taha Abdelmoutaleb Cherfia'
                        email = 'taha.cherfia@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/cherfia/jotenberg.git'
                    developerConnection = 'scm:git:ssh://github.com/cherfia/jotenberg.git'
                    url = 'https://github.com/cherfia/jotenberg'
                }
            }
        }
    }
}


javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}